plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.epages.restdocs-api-spec' version '0.19.1'
}

group = 'app'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0' // Swagger UI
	implementation 'io.jsonwebtoken:jjwt:0.12.6'
	implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.0'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'mysql:mysql-connector-java:8.0.33'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc' // Spring REST Docs: `.adoc` 형식 문서 생성
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.1' // 테스트 실행 시 OpenAPI 문서 만들기 위한 API 정보 수집

	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openapi3 {
	server = 'http://localhost:8080'
	title = 'Pigrest API'
	description = 'Pigrest API description'
	outputDirectory = "src/main/resources/public"
	version = '0.1.0'
	format = 'yaml'
}

tasks.register('bearerAuthentication') {
	mustRunAfter tasks.named('openapi3')
	doLast {
		def apiSpecFile = file('src/main/resources/public/openapi3.yaml')
		def contentToAdd =
				"  securitySchemes:\n" +
						"    bearerAuth:\n" +
						"      type: http\n" +
						"      scheme: bearer\n" +
						"      bearerFormat: JWT\n" +
						"\n" +
						"security:\n" +
						"  - bearerAuth: []"
		def existingContent = apiSpecFile.text
		def updatedContent = existingContent + contentToAdd
		apiSpecFile.withWriter('UTF-8') {
			writer -> writer.write(updatedContent)
		}
	}
}

tasks.named('build') {
	dependsOn tasks.named('openapi3')
	dependsOn tasks.named('bearerAuthentication')
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

postman {
	title = 'Pigrest API'
	version = '0.1.0'
	baseUrl = 'https://localhost:8080'
}